int numSnow = 100;
Snow[] snow;
int numLanterns = 50;
Lantern[] lanterns;

void setup() {
  size(800, 900);
  background(0);
  
  snow = new Snow[numSnow];
  for (int i = 0; i < numSnow; i++) {
    snow[i] = new Snow();
  }

  lanterns = new Lantern[numLanterns];
  for (int i = 0; i < numLanterns; i++) {
    lanterns[i] = new Lantern(random(width), height, random(2, 5));
  }
}

void draw() {
  background(0);
  
  for (int i = 0; i < numSnow; i++) {
    snow[i].fall();
    snow[i].display();
  }

  for (int j = 0; j < numLanterns; j++) {
    lanterns [j].rise();
    lanterns[j].display();
  }
}

//
class Snow {
  float x, y;
  float diameter;
  float speed;
  float angle; 
  
  Snow() {
    x = random(width);
    y = random(-500, -50);
    diameter = random(5, 15);
    speed = random(1, 3);
    angle = 5*PI/12; 
  }
  
  void fall() {
    x += cos(angle) * speed; 
    y += sin(angle) * speed; 
    
    if (y > height || x < 0 || x > width) {
      y = 0;
      x = random(width);
    }
  }
  
  void display() {
    noStroke();
    fill(255); 
    ellipse(x, y, diameter, diameter);
  }
}


//
class Lantern {
  float x, y;
  float size;
  float ySpeed;
  
  Lantern(float x, float y, float speed) {
    this.x = x;
    this.y = y;
    this.size = random(10, 20);
    this.ySpeed = speed;
  }
  
  void rise() {
    y -= ySpeed;
    
    if (y < 0) {
      y = height;
      x = random(width);
    }
  }
  
  void display() {
    fill(255, 165, 0); 
    noStroke();
    ellipse(x, y, size*1.25, size);
  }
}
